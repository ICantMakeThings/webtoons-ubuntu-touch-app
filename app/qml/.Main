import QtQuick 2.12
import Ubuntu.Components 1.3
import QtQuick.Window 2.2
import QtWebEngine 1.10

MainView {
    id: window
    applicationName: "webtoons.icmt"
    backgroundColor: "#323232"

    function isEpisode(url) {
        return url.toString().indexOf("/viewer?") !== -1
    }

    WebEngineProfile {
        id: mobileProfile
        storageName: "mobileProfile"
        persistentCookiesPolicy: WebEngineProfile.ForcePersistentCookies
        httpUserAgent: "Mozilla/5.0 (Linux; Android 8.0.0; Pixel) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/61.0.3163.98 Mobile Safari/537.36"
    }

    WebEngineProfile {
        id: readerProfile
        storageName: "readerProfile"
        persistentCookiesPolicy: WebEngineProfile.ForcePersistentCookies
        httpUserAgent: "Mozilla/5.0 (X11; Linux x86_64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/67.0.3396.99 Safari/537.36"
    }

// mobile view for browsing
    WebEngineView {
        id: mobileView
        anchors.fill: parent
        profile: mobileProfile
        visible: true
        zoomFactor: 1.0
        url: "https://m.webtoons.com/"

        settings {
            javascriptEnabled: true
            showScrollBars: false
        }

        onUrlChanged: {
            if (isEpisode(url)) {
                readerView.url = url
                readerView.visible = true
                mobileView.visible = false
            }
        }
    }


// desktop mode (so you dont get "Access free full episodes on the app" message)-
// in reader (plus scale to 45% to fit images properly)
    WebEngineView {
        id: readerView
        anchors.fill: parent
        profile: readerProfile
        visible: false
        zoomFactor: 0.45

        settings {
            javascriptEnabled: true
            showScrollBars: false
        }

        onUrlChanged: {
            if (!isEpisode(url)) {
                mobileView.url = url
                mobileView.visible = true
                readerView.visible = false
            }

            // block anything but webtoon (when accidentally clicking "share" or ads)
            if (!url.toString().startsWith("https://www.webtoons.com") &&
                !url.toString().startsWith("https://m.webtoons.com")) {
                console.log("Blocked external URL:", url)
                if (readerView.canGoBack) readerView.goBack()
                else {
                    mobileView.url = "https://m.webtoons.com/"
                    mobileView.visible = true
                    readerView.visible = false
                }
            }
        }

        userScripts: [
            WebEngineScript {
                name: "readerCSS"
                injectionPoint: WebEngineScript.DocumentReady
                worldId: WebEngineScript.MainWorld
                sourceCode: readerCss
            }
        ]
    }

// home button 
    Button {
        id: homebutton
        text: "Home"
        width: 70
        height: 35
        anchors.left: parent.left
        anchors.leftMargin: 10
        anchors.bottom: parent.bottom
        anchors.bottomMargin: 10
        z: 9999
        opacity: 0.8
        visible: readerView.visible

        onClicked: {
                mobileView.url = "https://m.webtoons.com/"
                mobileView.visible = true
                readerView.visible = false
        }
    }

// CSS ting to center book and make bg black in reader mode
    property string readerCss: "
(function() {
    var style = document.createElement('style');
    style.innerHTML = `
        html, body {
            margin: 0 !important;
            padding: 0 !important;
            overflow-x: hidden !important;
            background: #000 !important;
            display: flex !important;
            flex-direction: column !important;
            align-items: center !important;
        }

        img, canvas {
            max-width: 100% !important;
            height: auto !important;
            display: block !important;
            margin: 0 auto !important;
        }

        #viewerContainer,
        .viewer,
        .viewer_container {
            width: 100% !important;
            max-width: 100% !important;
            margin: 0 !important;
            padding: 0 !important;
        }

        .container, .scroll-wrapper {
            width: 100% !important;
            overflow-x: hidden !important;
        }
    `;
    document.head.appendChild(style);
})();
"
}
// might be of use? https://github.com/MrGautier256/webtoons-full-loader/blob/main/scripts/contentScript.js